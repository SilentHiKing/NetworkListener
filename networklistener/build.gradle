apply plugin: 'com.android.library'


android {
    compileSdkVersion 28



    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"


    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
}



apply plugin: 'com.novoda.bintray-release'//上传jcenter、maven

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

//解决 JavaDoc 中文注释生成失败的问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}


//发布到 Bintray
publish {

    userOrg = 'hurenbing'           //bintray.com 注册的用户名
    groupId = 'com.hiking.networklistener'   //以后访问 jcenter上此项目的路径，一般和库项目的包名一致
    artifactId = 'networklistener'       //bintray.com 创建的 Package 名
    publishVersion = '1.0.0'         //版本号
    desc = '网络变化监听' //版本说明，随意
    website = 'https://github.com/SilentHiKing/NetworkListener'    //关于这个开源项目的网站，随意
}

//./gradlew clean build generatePomFileForReleasePublication bintrayUpload -PbintrayUser=hurenbing -PbintrayKey=f4ed75884eb4be2872f51a33c0b5e15c7eac7a23 -PdryRun=false

